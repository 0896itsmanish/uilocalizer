dependencies {
    compile files("${System.getProperty('java.home')}/../lib/tools.jar")
    testCompile group: 'junit', name: 'junit', version: junitVersion
}

configurations {
    pluginBuild
    compile.extendsFrom(pluginBuild)
}

dependencies {
    pluginBuild project(':common')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'uilocalizer-processor'

            from components.java

            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar

            pom.withXml {
                asNode().dependencies.'*'.findAll() {
                    it.artifactId.text() == 'common'
                }.each() {
                    it.parent().remove(it)
                }
                // append additional metadata
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST

                    inceptionYear inceptionYear
                    organization {
                        name organization
                        url 'http://www.devexperts.com/'
                    }
                    scm {
                        connection 'scm:git:https://github.com/Devexperts/uilocalizer.git'
                        developerConnection 'scm:git:git@github.com/Devexperts/uilocalizer.git'
                        tag 'HEAD'
                        url 'https://github.com/Devexperts/uilocalizer.git'
                    }
                    issueManagement {
                        url 'https://github.com/Devexperts/uilocalizer/issues'
                    }
                    licenses {
                        license {
                            name 'MPL 2.0'
                            url 'http://mozilla.org/MPL/2.0/'
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
    repositories {
        maven {
            if (version.endsWith('SNAPSHOT')) {
                name 'evolution-snapshots'
                url 'https://maven.in.devexperts.com/content/repositories/sandbox-snapshot/'
            } else {
                name 'evolution'
                url 'https://maven.in.devexperts.com/content/repositories/sandbox/'
            }
        }
    }
}


jar {
    from configurations.pluginBuild.asFileTree.files.collect { zipTree(it) }
    //Required to collect @Localizable interface from the "uilocalizer-common" module
}